
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.COV_ПрохождениеВакцинацииОтCOVID") Тогда
		СтрРезультат = Документы.COV_СертификатОВакцинацииОтCOVID.ПолучитьВозможностьСозданияСертификата(ДанныеЗаполнения, ДанныеЗаполнения.Сотрудник);
		
		Если СтрРезультат.Возможность = Ложь Тогда 
			ВызватьИсключение СтрРезультат.ТекстПричины;
		КонецЕсли;
		Данные = СтрРезультат.Данные[0];
		Сотрудник = ДанныеЗаполнения.Сотрудник;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		ТипВакцины = Данные.ТипВакцины;
		СертификатДействителен = Истина;
		НачалоДействияСертификата = Данные.ДатаВакцинации;
		КонецДействияСертификата = ДобавитьМесяц(Данные.ДатаВакцинации, 12)-1;
		ОснованиеДействияССертификатом = "на основании вакцинации";
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Движения.COV_СертификатыОВакцинацииОтCOVID.Записывать = Истина;
	Движение = Движения.COV_СертификатыОВакцинацииОтCOVID.Добавить();
	Движение.Сотрудник = Сотрудник;
	Движение.ТипВакцины = ТипВакцины;
	Движение.ПериодДействияКонец = КонецДействияСертификата;
	Движение.Актуальный = СертификатДействителен;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Если СертификатДействителен
		И РежимЗаписи = РежимЗаписиДокумента.Проведение 
		И Не ПредыдущийСертификат.Пустая()
		Тогда 
		//РезультатДезактивации = ДезактивироватьПрежнийСертификат(Сотрудник, НачалоДействияСертификата, Ссылка);
		РезультатДезактивации = Документы.COV_СертификатОВакцинацииОтCOVID.ДезактивироватьСертификат(ПредыдущийСертификат);
		Если РезультатДезактивации.Успешно Тогда
			
			ТекстСообщения = "Устаревший сертификат деактивирован: " + Символы.ПС + Строка(РезультатДезактивации.Сертификат);
			Сообщение = Новый СообщениеПользователю;
			Сообщение.УстановитьДанные(РезультатДезактивации.Сертификат);
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Сообщить();
			
			ПредыдущийСертификат = РезультатДезактивации.Сертификат;
		Иначе 
			ТекстСообщения = "Не удалось деактивировать: " + Символы.ПС + Строка(РезультатДезактивации.Сертификат) + " " + РезультатДезактивации.ТекстОшибки;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.УстановитьДанные(РезультатДезактивации.Сертификат);
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ДезактивироватьПрежнийСертификат(Сотрудник, Дата, Ссылка)
	Перем ТекстСообщения;
	Перем прСертификат;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("Дата", НачалоДня(Дата));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	
	Запрос.Текст = "ВЫБРАТЬ
	|	COV_СертификатОВакцинацииОтCOVID.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.COV_СертификатОВакцинацииОтCOVID КАК COV_СертификатОВакцинацииОтCOVID
	|ГДЕ
	|	COV_СертификатОВакцинацииОтCOVID.Сотрудник = &Сотрудник
	|	И НАЧАЛОПЕРИОДА(COV_СертификатОВакцинацииОтCOVID.НачалоДействияСертификата, ДЕНЬ) <= &Дата
	|	И COV_СертификатОВакцинацииОтCOVID.СертификатДействителен
	|	И НЕ COV_СертификатОВакцинацииОтCOVID.ПометкаУдаления
	|	И ВЫБОР
	|			КОГДА &Ссылка = ЗНАЧЕНИЕ(Документ.COV_СертификатОВакцинацииОтCOVID.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ &Ссылка <> COV_СертификатОВакцинацииОтCOVID.Ссылка
	|		КОНЕЦ";
	
	Сертификат = Запрос.Выполнить().Выбрать();
	
	Если Сертификат.Следующий() Тогда 
		Результат = Документы.COV_СертификатОВакцинацииОтCOVID.ДезактивироватьСертификат(Сертификат.Ссылка, "Получен новый сертификат");
		Возврат Результат;
	КонецЕсли;;
	Возврат Неопределено;	
КонецФункции



